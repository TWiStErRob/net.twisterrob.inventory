if (project.hasProperty('libFromSources')) {
	evaluationDependsOn ':twister-lib-java'
}

buildscript {
	repositories {
		jcenter()
		maven { name 'twisterrob'; url 'http://localhost/maven' }
	}

	dependencies {
		//configurations.classpath.resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // -SNAPSHOT
		//noinspection GradleDynamicVersion
		classpath 'net.twisterrob.gradle:plugin:0.7.+'
	}
}

group 'net.twisterrob'
version '1.0'

apply plugin: 'com.android.library'
apply plugin: 'net.twisterrob.android-library'
apply from: 'http://localhost/maven/configure.gradle'

repositories {
	jcenter()
	maven { name 'twisterrob'; url 'http://localhost/maven' }
	maven { name 'glide'; url 'http://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
	//configurations.compile.resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // -SNAPSHOT
	if (project.hasProperty('libFromSources')) {
		compile project(':twister-lib-java')
	} else {
		compile 'net.twisterrob:twister-lib-java:1.0-SNAPSHOT'
	}
	def supportVersion = '24.0.0'
	compile "com.android.support:support-v4:${supportVersion}"
	compile "com.android.support:appcompat-v7:${supportVersion}"
	compile "com.android.support:cardview-v7:${supportVersion}"
	compile "com.android.support:recyclerview-v7:${supportVersion}"
	compile "com.android.support:design:${supportVersion}"
	compile 'com.rarepebble:colorpicker:1.7.0'
	//compile "com.android.support:palette-v7:${supportVersion}"

	// FIXME @Deprecated, use Glide
	compile 'com.jakewharton:disklrucache:2.0.2'
	compile 'com.caverock:androidsvg:1.2.1'
	compile 'com.github.bumptech.glide:glide:3.8.0-SNAPSHOT'

	testCompile('junit:junit:4.12') {
		exclude module: 'hamcrest'
		exclude module: 'hamcrest-core'
	}
	testCompile('org.hamcrest:hamcrest-junit:2.0.0.0') {
		exclude module: 'junit'
	}
	testCompile 'org.easymock:easymock:3.4'
}

android {
	// TODEL http://stackoverflow.com/a/30556702/253468
	useLibrary 'org.apache.http.legacy'
	testOptions {
		unitTests.returnDefaultValues = true
	}
}
