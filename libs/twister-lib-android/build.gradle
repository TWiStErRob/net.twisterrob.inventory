group = 'net.twisterrob.lib'
version = '1.0'

apply plugin: 'net.twisterrob.android-library'
apply from: 'http://localhost/maven/configure.gradle'

dependencies {
	//configurations.implementation.resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // -SNAPSHOT
	implementation 'net.twisterrob.lib:twister-libs-java:1.0-SNAPSHOT'
	implementation 'net.twisterrob.lib:twister-lib-android-defs'
	implementation 'net.twisterrob.lib:twister-lib-android-logging'
	implementation 'net.twisterrob.lib:twister-lib-android-basics'
	implementation 'net.twisterrob.lib:twister-lib-android-stringers'
	implementation "com.android.support:support-v4:${VERSION_SUPPORT}"
	implementation "com.android.support:appcompat-v7:${VERSION_SUPPORT}"
	implementation "com.android.support:cardview-v7:${VERSION_SUPPORT}"
	implementation "com.android.support:recyclerview-v7:${VERSION_SUPPORT}"
	implementation "com.android.support:design:${VERSION_SUPPORT}"
	//implementation "com.android.support:palette-v7:${VERSION_SUPPORT}"

	// FIXME @Deprecated, use Glide
	implementation 'com.jakewharton:disklrucache:2.0.2'
	implementation 'com.rarepebble:colorpicker:1.7.0'

	apply from: "${rootDir}/gradle/testCompile.gradle", to: project
}

android {
	defaultConfig {
		minSdkVersion 10
		multiDexEnabled = true
		dependencies {
			implementation "com.android.support:multidex:${VERSION_MULTIDEX}"
		}
	}
	lintOptions {
		checkAllWarnings = true
		checkDependencies = true
	}
	testOptions {
		unitTests.returnDefaultValues = true
	}
}

def mainProject = project
allprojects { project ->
	project.repositories {
		jcenter()
		maven { name = 'twisterrob'; url = 'http://localhost/maven' }
	}
	project.afterEvaluate {
		project.android.lintOptions.lintConfig = mainProject.file("config/lint/lint.xml")
		project.android.lintOptions.baseline mainProject.file("config/lint/lint-baseline-${project.name}.xml")

		com.android.build.gradle.api.AndroidSourceSet androidTest = project.android.sourceSets.androidTest
		if (androidTest.java.sourceFiles.empty) {
			logger.info "Disabling AndroidTest tasks in ${project.path} as it has no sources in ${androidTest.java.srcDirs}"
			project.tasks.whenTaskAdded { task ->
				if (task.name.contains("AndroidTest")) {
					task.enabled = false
				}
			}
		}
	}
}

task cleanFull {
	dependsOn allprojects*.tasks*.findByPath('clean')
}
