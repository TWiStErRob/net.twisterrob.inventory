buildscript {
	repositories {
		google()
		jcenter()
		maven { name = "twisterrob"; url = "http://localhost/maven"; allowInsecureProtocol = true }
		maven { name = "Gradle libs (for Kotlin-DSL)"; url = "https://repo.gradle.org/gradle/libs-releases-local/" }
	}
	dependencies {
		configurations["classpath"].resolutionStrategy.cacheChangingModulesFor(0, "seconds") // -SNAPSHOT
		classpath "net.twisterrob.gradle:plugin:${VERSION_TWISTER_GRADLE}"
	}
}

subprojects { Project project ->
	project.repositories {
		google()
		jcenter()
	}
	project.configurations.all {
		if (it.name == 'lintClassPath') return
//		it.resolutionStrategy.failOnVersionConflict()
	}
	project.apply from: rootProject.file("gradle/substitutions.gradle"), to: project

	project.plugins.withId("net.twisterrob.android-library") {
		project.android.buildFeatures.buildConfig = false
		project.android.twisterrob.decorateBuildConfig = false
	}
	project.afterEvaluate {
		project.android.compileSdkVersion = 28
		project.android.defaultConfig.minSdkVersion = 14
		project.android.lintOptions.lintConfig = rootProject.file("config/lint/lint.xml")
		project.android.lintOptions.baseline rootProject.file("config/lint/lint-baseline-${project.name}.xml")

		com.android.build.gradle.api.AndroidSourceSet androidTest = project.android.sourceSets.androidTest
		if (androidTest.java.sourceFiles.empty) {
			logger.info "Disabling AndroidTest tasks in ${project.path} as it has no sources in ${androidTest.java.srcDirs}"
			project.tasks.whenTaskAdded { task ->
				if (task.name.contains("AndroidTest")) {
					task.enabled = false
				}
			}
		}
	}
}

tasks.register("cleanFull") {
	// subprojects*.tasks*.named("clean") is not available at this point
	dependsOn(subprojects.collect { "${it.path}:clean" })
}

tasks.register("connectedCheck").configure {
	dependsOn(subprojects.collect { "${it.path}:connectedCheck" })
}

evaluationDependsOnChildren() // TODO somehow remove this
allprojects \
	.collectMany { it.tasks.withType(com.android.build.gradle.internal.tasks.DeviceProviderInstrumentTestTask) } \
	.inject { prev, curr -> curr.configure { it.mustRunAfter prev }; return prev }
