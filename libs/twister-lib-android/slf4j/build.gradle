buildscript {
	apply from: rootProject.file("gradle/repos.gradle"), to: buildscript
	dependencies {
		//configurations.classpath.resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // -SNAPSHOT
		classpath "net.twisterrob.gradle:plugin:${VERSION_TWISTER_GRADLE}"
	}
}

group = 'net.twisterrob'
version = '1.0'

apply plugin: 'com.android.library'
apply plugin: 'net.twisterrob.android-library'
apply from: 'http://localhost/maven/configure.gradle'

repositories {
	jcenter()
	maven { name = 'twisterrob'; url = 'http://localhost/maven' }
}

dependencies {
	implementation "com.android.support:support-annotations:${VERSION_SUPPORT}"
	implementation "org.slf4j:slf4j-api:${VERSION_SLF4J}"
}

android {
	defaultConfig.minSdkVersion 9
	lintOptions {
		tasks.lint.enabled = false
	}
}

task jar(type: Jar, dependsOn: 'assembleRelease') {
	exclude "**/R.class"
	exclude "**/R\$*.class"
	exclude "**/BuildConfig.class"
	//from files(project.android.sourceSets.main.output)
}
afterEvaluate {
	com.android.build.gradle.LibraryExtension android = project.android
	def dirs = android
			.libraryVariants
			.findAll { it.buildType.name == android.buildTypes.release.name }
			.collect { it.javaCompiler.destinationDir }
	tasks.jar.from files(dirs)
}
artifacts {
	// reference as dependencies { runtimeOnly project(path: ':twister-lib-android-slf4j', configuration: 'jar') }
	configurations.create 'jar'
	add('jar', tasks.jar)
}
