buildscript {
	repositories {
		jcenter()
		google()
		maven { name = 'twisterrob'; url = 'http://localhost/maven' }
		maven { name = "Gradle libs (for Kotlin-DSL)"; url = "https://repo.gradle.org/gradle/libs-releases-local/" }
	}
	dependencies {
		//configurations.classpath.resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // -SNAPSHOT
		classpath "net.twisterrob.gradle:plugin:${VERSION_TWISTER_GRADLE}"
	}
}

group = 'net.twisterrob.lib'
version = '1.0'

allprojects { project ->
	repositories {
		jcenter()
	}
	configurations.all {
		if (it.name == 'lintClassPath') return
		it.resolutionStrategy.failOnVersionConflict()
	}
	apply plugin: 'net.twisterrob.java-library'
	apply from: 'http://localhost/maven/configure.gradle'
	apply from: rootProject.file("gradle/substitutions.gradle")
	dependencies {
		apply from: "${rootDir}/gradle/testCompile.gradle", to: project

		api "org.slf4j:slf4j-api:${VERSION_SLF4J}"
		testImplementation "org.slf4j:slf4j-simple:${VERSION_SLF4J}"
		// TODO change to implementation (first step: exclude in Android)
		api 'com.google.code.findbugs:jsr305:3.0.2'
	}
}

dependencies {
	// TODO remove all dependencies of this project and use specific modules
	api 'net.twisterrob.lib:twister-lib-core'
	api 'net.twisterrob.lib:twister-lib-collect'
	api 'net.twisterrob.lib:twister-lib-stringer'
	api 'net.twisterrob.lib:twister-lib-java'
}

tasks.register("cleanFull") {
	dependsOn allprojects*.tasks*.named("clean")
}
