if (project.hasProperty('libFromSources')) {
	evaluationDependsOn ':twister-lib-java'
	evaluationDependsOn ':twister-lib-android'
}

buildscript {
	apply from: 'gradle/buildscript.gradle', to: buildscript
}

apply plugin: 'com.android.application'
apply plugin: 'net.twisterrob.android-app'
apply from: 'gradle/mapping.gradle'

apply plugin: InventoryDatabasePlugin

databaseEntities {
	categories {
		input = file(/i:\src\main\res\values\strings_Categories.xml/)
		output = file(/i:\src\main\assets\MagicHomeInventory.data.Categories.sql/)
		iconFolder = file(/i:\src\main\res\raw/)
		conversion = "SQL"
	}
}
android.applicationVariants.all { com.android.build.gradle.api.ApplicationVariant variant ->
	variant.mergeAssets.dependsOn tasks.generateDataBase
}

repositories {
	jcenter()
	maven { name 'twisterrob'; url 'http://localhost/maven' }
	maven { name 'glide'; url 'http://oss.sonatype.org/content/repositories/snapshots' }
//	maven { name 'idescout'; url 'http://www.idescout.com/maven/repo/' }
}

dependencies {
	//configurations.compile.resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // -SNAPSHOT
	if (project.hasProperty('libFromSources')) {
		compile project(':twister-lib-java')
		compile project(':twister-lib-android')
	} else {
		compile('net.twisterrob:twister-lib-android:1.0-SNAPSHOT')
		compile('net.twisterrob:twister-lib-java:1.0-SNAPSHOT')
	}
	// TOFIX otherwise VariantFragment descendants don't see Fragment and its methods (Cannot access FQCN errors in IDEA)
	def supportVersion = '23.4.0'
	compile "com.android.support:support-v4:${supportVersion}"
	compile "com.android.support:appcompat-v7:${supportVersion}"
	compile "com.android.support:design:${supportVersion}"
//	compile "com.android.support:cardview-v7:${supportVersion}"
	compile "com.android.support:recyclerview-v7:${supportVersion}"
//	debugCompile 'com.facebook.stetho:stetho:1.3.1'
//	debugCompile 'com.facebook.stetho:stetho-js-rhino:1.3.1'
//	debugCompile 'com.idescout.sql:sqlscout-server:1.0'
	testCompile('junit:junit:4.12') {
		exclude module: 'hamcrest'
		exclude module: 'hamcrest-core'
	}
	testCompile('org.hamcrest:hamcrest-junit:2.0.0.0') {
		exclude module: 'junit'
	}
}

android {
	defaultConfig {
		applicationId 'net.twisterrob.inventory'
		version {
			versionFile = null
			major = 1
		}
		resConfigs "en_GB", "en_NZ", "en_AU" //, "hu"
	}
	lintOptions {
		//abortOnError false
	}
	buildTypes {
		release {
			//debuggable true
			shrinkResources true // http://tools.android.com/tech-docs/new-build-system/resource-shrinking
		}
		debug {
			//minifyEnabled true
		}
	}
	aaptOptions {
		noCompress 'svg' // Resources.openRawResourceFd can only open AssetFileDescriptor for uncompressed resources
		def defaultIgnoreAssetsPattern = '!.svn:!.git:!.ds_store:!*.scc:.*:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~'
		ignoreAssetsPattern defaultIgnoreAssetsPattern + ':!descript.ion:!*.yuml:!*.yuml.png'
	}
	sourceSets.main.res.srcDir 'src/main/res-icons'
}
