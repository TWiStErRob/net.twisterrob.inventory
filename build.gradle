apply plugin: 'net.twisterrob.root'
apply plugin: 'net.twisterrob.quality'
apply plugin: 'idea'

allprojects {
	repositories {
		jcenter()
		maven { name = 'twisterrob'; url = 'http://localhost/maven' }
		//maven { name = 'sonatype'; url = 'http://oss.sonatype.org/content/repositories/snapshots' }
		//maven { name = 'idescout'; url = 'http://www.idescout.com/maven/repo/' }
	}
	apply from: rootProject.file('gradle/substitutions.gradle')
	configurations.all {
		if (it.name == 'lintClassPath') return
		it.resolutionStrategy.failOnVersionConflict()
	}
	afterEvaluate {
		def android = project.extensions.findByName("android")
		if (android != null) {
			android.compileSdkVersion = 28
			android.defaultConfig.minSdkVersion = 21
		}
	}
	tasks.withType(JavaCompile) {
		it.options.compilerArgs += [
				// enable all warnings
				"-Xlint:all",
				// fail build when warnings pop up
				"-Werror",
				// Ignore for now, until understanding what it means.
				// warning: [varargs] Varargs method could cause heap pollution from non-reifiable varargs parameter params
				"-Xlint:-varargs",
				// TODO check my plugin, maybe java-library and java are not compatible there
				// warning: [options] bootstrap class path not set in conjunction with -source 1.7
				"-Xlint:-options",
		]

		if (it.name.endsWith("UnitTestJavaWithJavac")
				|| it.name.endsWith("AndroidTestJavaWithJavac")
				|| it.path.startsWith(':android:database:test_helpers:')) {
			it.options.compilerArgs += [
					// Google's compilers emit some weird stuff (espresso, dagger, etc.)
					// warning: [classfile] MethodParameters attribute introduced in version 52.0 class files is ignored in version 51.0 class files
					"-Xlint:-classfile",
			]
		}
	}
}

evaluationDependsOnChildren()
def connectedTasks = allprojects.collectMany { it.tasks.withType(com.android.build.gradle.internal.tasks.DeviceProviderInstrumentTestTask) }
connectedTasks.inject { prev, curr -> curr.configure { it.mustRunAfter prev }; return prev }

// Disable these temporarily because `gradlew :android:connectedCheck` fails with:
// > Included build task ':about:connectedDebugAndroidTest' was never scheduled for execution.
def includedConnectedTasks = [
//		gradle.includedBuild("twister-libs-android").task(":espresso:connectedDebugAndroidTest"),
//		gradle.includedBuild("twister-libs-android").task(":about:connectedDebugAndroidTest"),
]
connectedTasks.each { it.configure { it.mustRunAfter(includedConnectedTasks) } }

tasks.register("connectedCheck") {
//	dependsOn(gradle.includedBuild("twister-libs-android").task(':connectedCheck'))
}

task clean(type: Delete) {
	dependsOn gradle.includedBuilds*.task(':cleanFull')
	delete rootProject.buildDir
}

idea {
	module {
		excludeDirs += [
				// local untracked folder for junk
				rootProject.file("temp"),
				// generated by mergeAndroidReports
				new File(rootProject.buildDir, "androidTest-results"),
				new File(rootProject.buildDir, "reports/androidTests")
		]
	}
}

// run `gradlew releaseCheck -x check -x connectedCheck` to just get the output of this
// CONSIDER deprecate this against `gradlew connectedCheck --continue mergeAndroidReports` from 'android-reporting' 
task releaseCheck() {
	dependsOn getTasksByName('check', true)
	dependsOn ':android:connectedCheck'
	doLast {
		def report = { task, type, loc ->
			if (file(loc).exists()) {
				println "${task.path} ${type}: ${loc}"
			}
		}
		println "================ Report Locations ================"
		/* TODO AGP 2.x to 3.x breaking changes
		getTasksByName('lint', true).each {
			new LintHelper(it).print(report)
		}
		*/
		getAllTasks(true)
				.collectMany { it.value }
				.findAll { it instanceof Test }
				.each { Test task -> report(task, 'test', task.reports.html.entryPoint) }
	}
}
/* TODO AGP 2.x to 3.x breaking changes 
//apply plugin: 'android-reporting'
//when accessing project.android to set compileSdkVerison,
//may need to exclude this android.class.simpleName != "TestOptions_Decorated"
    
import com.android.build.gradle.internal.dsl.LintOptions
import com.android.build.gradle.tasks.Lint

class LintHelper {
	Lint task
	LintHelper(Task task) {
		this.task = (Lint)task
	}
	private static lintField(Object task, String name) {
		def field = Lint.class.getDeclaredField(name)
		field.accessible = true
		return field.get(task)
	}
	def output(ext) {
		def fatal = lintField(task, 'mFatalOnly') as boolean
		File out
		out = LintOptions.createOutputPath(task.project, 'release', ".${ext}", fatal)
		if (out.exists()) {
			return out
		}
		out = LintOptions.createOutputPath(task.project, 'debug', ".${ext}", fatal)
		if (out.exists()) {
			return out
		}
		return LintOptions.createOutputPath(task.project, task.variantName, ".${ext}", fatal)
	}
	def print(reporter) {
		def lint = lintField(task, 'mLintOptions') as com.android.builder.model.LintOptions
		if (lint.textReport) {
			reporter(task, 'text', lint.textOutput ?: output('txt'))
		}
		if (lint.htmlReport) {
			reporter(task, 'html', lint.htmlOutput ?: output('html'))
		}
		if (lint.xmlReport) {
			reporter(task, 'xml', lint.xmlOutput ?: output('xml'))
		}
	}
}
*/
