buildscript {
	apply from: rootProject.file("gradle/repos.gradle"), to: buildscript
	dependencies {
		//configurations.classpath.resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // -SNAPSHOT
		classpath "net.twisterrob.gradle:plugin:${VERSION_TWISTER_GRADLE}"
	}
}

apply plugin: 'net.twisterrob.root'

allprojects {
	repositories {
		jcenter()
		maven { name = 'twisterrob'; url = 'http://localhost/maven' }
		maven { name = 'sonatype'; url = 'http://oss.sonatype.org/content/repositories/snapshots' }
		//maven { name = 'idescout'; url = 'http://www.idescout.com/maven/repo/' }
	}
	apply from: rootProject.file('gradle/includedBuilds/substitute_twister-libs-java.gradle')
	apply from: rootProject.file('gradle/includedBuilds/substitute_twister-libs-android.gradle')
	apply from: rootProject.file('gradle/substitutions.gradle')
	configurations.all {
		if (it.name == 'lintClassPath') return
		//it.resolutionStrategy.failOnVersionConflict()
	}

	tasks.withType(JavaCompile) {
		it.options.compilerArgs += [
				// enable all warnings
				"-Xlint:all",
				// fail build when warnings pop up
				"-Werror",
				// Ignore for now, until understanding what it means.
				// warning: [varargs] Varargs method could cause heap pollution from non-reifiable varargs parameter params
				"-Xlint:-varargs",
				// TODO check my plugin, maybe java-library and java are not compatible there
				// warning: [options] bootstrap class path not set in conjunction with -source 1.7
				"-Xlint:-options",
		]

		if (it.name.endsWith("AndroidTestJavaWithJavac") || it.path.contains(":twister-lib-android:")) {
			it.doFirst {
				// TODO move to plugin
				def duplicates = it
						.options.compilerArgs
						.grep { it.startsWith("-Xlint:-") }
						.collect { "-Xlint:${it.substring("-Xlint:-".length())}" }
				// if something is both disabled and enabled, make the disable win
				it.options.compilerArgs -= duplicates
				logger.debug "${it.path}: ${it.options.compilerArgs}"
			}
			it.options.compilerArgs += [
					// Google's compilers emit some weird stuff (espresso, dagger, etc.)
					// warning: [classfile] MethodParameters attribute introduced in version 52.0 class files is ignored in version 51.0 class files
					"-Xlint:-classfile",
					// TODO There are some more left: registerIdlingResources and ViewActivityActor.refresh(), RuleChain from JUnit
					"-Xlint:-deprecation",
			]
		}
	}
}

//tasks.withType(com.android.build.gradle.internal.tasks.DeviceProviderInstrumentTestTask)
[
		':libs:twister-lib-android:espresso',
		':libs:twister-lib-android:about',
		':android'
]
		.collect { tasks.findByPath("$it:connectedDebugAndroidTest") }
		.grep { it != null }
		.inject { prev, curr -> curr.mustRunAfter prev; return prev }

task clean(type: Delete) {
	dependsOn gradle.includedBuilds*.task(':cleanFull')
	delete rootProject.buildDir
}

// run `gradlew releaseCheck -x check -x connectedCheck` to just get the output of this
task releaseCheck() {
	dependsOn getTasksByName('check', true)
	dependsOn ':android:connectedCheck'
	doLast {
		def report = { task, type, loc ->
			if (file(loc).exists()) {
				println "${task.path} ${type}: ${loc}"
			}
		}
		println "================ Report Locations ================"
		/* TODO AGP 2.x to 3.x breaking changes
		getTasksByName('lint', true).each {
			new LintHelper(it).print(report)
		}
		*/
		getAllTasks(true)
				.collectMany { it.value }
				.findAll { it instanceof Test }
				.each { Test task -> report(task, 'test', task.reports.html.entryPoint) }
	}
}
/* TODO AGP 2.x to 3.x breaking changes 
import com.android.build.gradle.internal.dsl.LintOptions
import com.android.build.gradle.tasks.Lint

class LintHelper {
	Lint task
	LintHelper(Task task) {
		this.task = (Lint)task
	}
	private static lintField(Object task, String name) {
		def field = Lint.class.getDeclaredField(name)
		field.accessible = true
		return field.get(task)
	}
	def output(ext) {
		def fatal = lintField(task, 'mFatalOnly') as boolean
		File out
		out = LintOptions.createOutputPath(task.project, 'release', ".${ext}", fatal)
		if (out.exists()) {
			return out
		}
		out = LintOptions.createOutputPath(task.project, 'debug', ".${ext}", fatal)
		if (out.exists()) {
			return out
		}
		return LintOptions.createOutputPath(task.project, task.variantName, ".${ext}", fatal)
	}
	def print(reporter) {
		def lint = lintField(task, 'mLintOptions') as com.android.builder.model.LintOptions
		if (lint.textReport) {
			reporter(task, 'text', lint.textOutput ?: output('txt'))
		}
		if (lint.htmlReport) {
			reporter(task, 'html', lint.htmlOutput ?: output('html'))
		}
		if (lint.xmlReport) {
			reporter(task, 'xml', lint.xmlOutput ?: output('xml'))
		}
	}
}
*/
