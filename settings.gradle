import org.gradle.api.initialization.resolve.RepositoriesMode

pluginManagement {
	includeBuild("gradle/plugins-inventory")
	repositories {
		google()
		mavenCentral()
	}
}

dependencyResolutionManagement {
	repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)
	repositories {
		google()
		mavenCentral()
		maven { // jcenter() deprecated
			name = "jcenter"
			setUrl("https://jcenter.bintray.com/")
			content {
				// Can't update flexbox yet to its 3.x version, because it ships with Java 9 bytecode.
				includeModule("com.google.android", "flexbox")
			}
		}
	}
}

rootProject.name = 'Inventory'

include ':android'
include ':android:base'
include ':android:test_helpers'
includeWithContract ':android:database'
include ':android:database:test_helpers'
include ':android:preferences'
include ':android:data'
include ':android:data:svg'
include ':android:backup'
include ':tools:transform'
include ':libs'

def includeWithContract(String projectPath) {
	include projectPath
	def contractProjectPath = "${projectPath}-contract"
	include contractProjectPath
	project(contractProjectPath).projectDir = new File(project(projectPath).projectDir, 'contract')
}

includeBuild("libs/twister-lib-java") { build ->
	name = "twister-libs-java"
	apply from: new File(build.projectDir, "settings.substitutions.gradle"), to: build
}

includeBuild("libs/twister-lib-android") { build ->
	name = "twister-libs-android"
	apply from: new File(build.projectDir, "settings.substitutions.gradle"), to: build
}

dependencyResolutionManagement {
	versionCatalogs {
		create("utp") {
			from(files("gradle/utp.versions.toml"))
		}
	}
}
