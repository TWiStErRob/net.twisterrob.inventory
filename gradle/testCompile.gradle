def isAndroid = plugins.hasPlugin('com.android.application') || plugins.hasPlugin('com.android.library')

repositories {
	maven { name 'sonatype'; url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

configurations.all {
	resolutionStrategy {
		force "junit:junit:${VERSION_JUNIT}"
		force "org.mockito:mockito-core:${VERSION_MOCKITO}"
		force 'org.objenesis:objenesis:2.4' // 2.1 vs 2.2
		force 'com.thoughtworks.xstream:xstream:1.4.9' // 1.4.8 vs 1.4.9
		dependencySubstitution {
			substitute(module('org.hamcrest:hamcrest-core'))
					.with module("org.hamcrest:java-hamcrest:${VERSION_HAMCREST}")
			substitute(module('org.hamcrest:hamcrest-library'))
					.with module("org.hamcrest:java-hamcrest:${VERSION_HAMCREST}")
		}
	}
}

if (isAndroid) {
	configurations {
		// need to go for all, because of the hidden _debugAndroidTestCompile and friends
		/*[ androidTestCompile ].each*/ all {
			it.resolutionStrategy {
				force 'com.google.code.findbugs:jsr305:3.0.0' // 2.0.1 vs 3.0.0
				// Force some dependencies to be consistent between compile and androidTestCompile
				dependencySubstitution {
					[ 'support-annotations', 'support-v4', 'appcompat-v7', 'support-v13',
					  'design', 'customtabs', 'preference-v7', 'preference-v14',
					  'recyclerview-v7', 'cardview-v7', 'gridlayout-v7', 'percent', 'palette-v7',
					  'mediarouter-v7', 'leanback-v17', 'preference-leanback-v17', 'recommendation',
					  'support-vector-drawable', 'animated-vector-drawable'
					].each {
						force "com.android.support:${it}:${VERSION_SUPPORT}"
						substitute(module("com.android.support:${it}"))
								.with module("com.android.support:${it}:${VERSION_SUPPORT}")
					}
				}
			}
		}
	}
}

dependencies {
	testCompile "junit:junit:${VERSION_JUNIT}"
	testCompile "org.hamcrest:hamcrest-junit:${VERSION_HAMCREST}"
	testCompile "org.mockito:mockito-core:${VERSION_MOCKITO}"
	testCompile "org.powermock:powermock-api-mockito2:${VERSION_POWERMOCK}"
	testCompile "org.powermock:powermock-module-junit4:${VERSION_POWERMOCK}"
	testCompile "org.powermock:powermock-module-junit4-rule:${VERSION_POWERMOCK}"
	testCompile "org.powermock:powermock-module-junit4-rule-agent:${VERSION_POWERMOCK}"
	testCompile "org.powermock:powermock-classloading-xstream:${VERSION_POWERMOCK}"
	testCompile 'com.shazam:gwen:1.0.2' // Gwen.given, Gwen.when, Gwen.then 
	testCompile 'org.slf4j:slf4j-simple:1.7.21'
	testCompile 'com.diffplug.durian:durian:3.4.0'
	testCompile 'com.github.stefanbirkner:fishbowl:1.4.0' // exceptionThrownBy (assertThrows-like)
//	testCompile 'cglib:cglib:3.2.4'
//	testCompile 'pl.pragmatists:JUnitParams:1.0.4'
//	testCompile 'nl.jqno.equalsverifier:equalsverifier:1.7.2'

	if (isAndroid) {
		testCompile("org.robolectric:robolectric:3.1.2") {
			exclude group: 'org.codehaus.plexus', module: 'plexus-utils'
		}
		testCompile 'org.codehaus.plexus:plexus-utils:1.5.15' // robolectric has inconsistent dependencies

//	configurations
//			.compile
//			.resolvedConfiguration
//			.resolvedArtifacts
//			.collect { it.id.componentIdentifier }
//			.grep { it instanceof ModuleComponentIdentifier }
//			.grep { it.group == 'com.android.support' }
//			.each { androidTestCompile it.displayName }

		androidTestCompile "junit:junit:${VERSION_JUNIT}"
		androidTestCompile "org.hamcrest:hamcrest-junit:${VERSION_HAMCREST}"
//		androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.0'
//		androidTestCompile 'com.google.dexmaker:dexmaker:1.0'
		// Android JUnit Runner
		androidTestCompile 'com.android.support.test:runner:0.5'

		// JUnit4 Rules
		androidTestCompile 'com.android.support.test:rules:0.5'

		// Espresso core
		androidTestCompile "com.android.support.test.espresso:espresso-core:${VERSION_ESPRESSO}"
		// Espresso-contrib for DatePicker, RecyclerView, Drawer actions, Accessibility checks, CountingIdlingResource
		androidTestCompile("com.android.support.test.espresso:espresso-contrib:${VERSION_ESPRESSO}") {
			// Need to ignore support dependencies, otherwise weird exceptions happen, for example:
			// * Error inflating class android.support.v7.widget.CardView Resource is not a ColorStateList (color or path)
			// * NullPointerException (mDecorContentParent.setWindowCallback) at AppCompatDelegateImplV7.createSubDecor
			exclude group: 'com.android.support'
		}
		// Espresso-web for WebView support
		androidTestCompile "com.android.support.test.espresso:espresso-web:${VERSION_ESPRESSO}"
		// Espresso-intents for capturing Intents and mocking onActivityResult
		androidTestCompile "com.android.support.test.espresso:espresso-intents:${VERSION_ESPRESSO}"
		// Espresso-idling-resource for synchronization with background jobs
		androidTestCompile "com.android.support.test.espresso:espresso-idling-resource:${VERSION_ESPRESSO}"
		// UI Automator @SdkSuppress(minSdkVersion = VERSION_CODES.JELLY_BEAN_MR2)
		androidTestCompile "com.android.support.test.uiautomator:uiautomator-v18:2.+"
	}
}
