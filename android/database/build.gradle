plugins {
	id("net.twisterrob.inventory.android.module")
}

dependencies {
	api project(':android:database-contract')
	implementation project(':android:base')

	implementation "androidx.annotation:annotation:${VERSION_ANDROIDX_ANNOTATION}"
	implementation "net.twisterrob.lib:twister-lib-android-monolith"
	implementation "net.twisterrob.lib:twister-lib-android-basics"
	implementation "net.twisterrob.lib:twister-lib-core"
	implementation "net.twisterrob.lib:twister-lib-java"
	implementation "androidx.preference:preference:${VERSION_ANDROIDX_PREFERENCE}"
}

apply plugin: "net.twisterrob.inventory.database"

databaseEntities {
	categories {
		//noinspection UnnecessaryQualifiedReference,GrDeprecatedAPIUsage REPORT cannot replace with new interface, missing methods
		com.android.build.gradle.api.AndroidSourceSet data_ss = evaluationDependsOn(':android:data').android.sourceSets.main
		def my_ss = android.sourceSets.main
		input = file(new File(data_ss.res.srcDirs.first(), 'values/strings_Categories.xml'))
		output = file(new File(my_ss.assets.srcDirs.first(), 'MagicHomeInventory.data.Categories.sql'))
		iconFolder = file(new File(data_ss.res.srcDirs.first(), 'raw'))
		conversion = "SQL"
	}
}
android.libraryVariants.all { com.android.build.gradle.api.LibraryVariant variant ->
	variant.mergeAssetsProvider.configure { it.dependsOn(tasks.generateDataBase) }
}

tasks.register("testDatabase", Exec) {
	onlyIf { System.getProperty("os.name").startsWith("Windows") }
	dependsOn(tasks.generateDataBase)
	def outputDir = file("${buildDir}/database") // TODO why can't I use File(File, String) ctor?
	workingDir outputDir
	inputs.file('test-db.bat')
	inputs.file('test-db.sql')
	inputs.dir(android.sourceSets.main.assets.srcDirs[0])
	inputs.dir(android.sourceSets.debug.assets.srcDirs[0])
	outputs.dir(outputDir)
	commandLine 'cmd', '/c', file('test-db.bat'), projectDir, outputDir
	tasks.findByPath('check').dependsOn(it)
}
