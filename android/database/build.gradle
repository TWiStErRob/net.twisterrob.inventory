plugins {
	id("net.twisterrob.inventory.android.module")
	id("net.twisterrob.inventory.database")
	id("net.twisterrob.inventory.build.hilt")
}

dependencies {
	api project(':android:database-contract')
	implementation project(':android:base')
	implementation project(':android:preferences')

	implementation "net.twisterrob.lib:twister-lib-android-monolith"
	implementation "net.twisterrob.lib:twister-lib-android-basics"
	implementation "net.twisterrob.lib:twister-lib-android-settings"
	implementation "net.twisterrob.lib:twister-lib-core"
	implementation "net.twisterrob.lib:twister-lib-java"
	implementation("net.twisterrob.lib:twister-lib-general")
	implementation(libs.androidx.preference)
}

databaseEntities {
	categories {
		//noinspection UnnecessaryQualifiedReference,GrDeprecatedAPIUsage REPORT cannot replace with new interface, missing methods
		com.android.build.gradle.api.AndroidSourceSet data_ss = evaluationDependsOn(':android:data').android.sourceSets.main
		input.set(file(new File(data_ss.res.srcDirs.first(), 'values/strings_Categories.xml')))
		assetPath.set("MagicHomeInventory.data.Categories.sql")
		iconFolder.set(file(new File(data_ss.res.srcDirs.first(), 'raw')))
		conversion.set("SQL")
	}
}
android.libraryVariants.all { com.android.build.gradle.api.LibraryVariant variant ->
	variant.mergeAssetsProvider.configure { it.dependsOn(tasks.generateDataBase) }
}

tasks.register("testDatabase", Exec) {
	onlyIf { System.getProperty("os.name").startsWith("Windows") }
	dependsOn(tasks.generateDataBase)
	def outputDir = file("${buildDir}/database") // TODO why can't I use File(File, String) ctor?
	workingDir outputDir
	inputs.file('test-db.bat')
	inputs.file('test-db.sql')
	inputs.dir(android.sourceSets.main.assets.srcDirs[0])
	inputs.dir(android.sourceSets.debug.assets.srcDirs[0])
	outputs.dir(outputDir)
	commandLine 'cmd', '/c', file('test-db.bat'), projectDir, outputDir
	tasks.findByPath('check').dependsOn(it)
}
