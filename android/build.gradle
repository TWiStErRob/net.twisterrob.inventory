apply plugin: 'net.twisterrob.android-app'
apply plugin: "net.twisterrob.inventory.mapping"
apply plugin: "net.twisterrob.inventory.androidTest"

dependencies {
	implementation project(':android:base')
	implementation project(':android:database')
	implementation project(':android:backup')
	implementation project(':android:preferences')
	testImplementation project(':android:test_helpers')

	//configurations.implementation.resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // -SNAPSHOT
	implementation('net.twisterrob.lib:twister-lib-android-monolith')
	implementation "net.twisterrob.lib:twister-lib-android-glide3"
	implementation('net.twisterrob.lib:twister-lib-general')
	implementation 'net.twisterrob.lib:twister-lib-android-basics'
	implementation 'net.twisterrob.lib:twister-lib-android-widgets'
	implementation 'net.twisterrob.lib:twister-lib-android-about'
	implementation 'net.twisterrob.lib:twister-lib-android-logging'
	implementation 'net.twisterrob.lib:twister-lib-android-stringers'
	implementation 'net.twisterrob.lib:twister-lib-android-capture_image'
	runtimeOnly('net.twisterrob.lib:twister-lib-android-slf4j')

	// TOFIX otherwise VariantFragment descendants don't see Fragment and its methods (Cannot access FQCN errors in IDEA)
	implementation "com.android.support:support-v4:${VERSION_SUPPORT}"
	implementation "com.android.support:support-annotations:${VERSION_SUPPORT}"
	implementation "com.android.support:appcompat-v7:${VERSION_SUPPORT}"
	implementation "com.android.support:design:${VERSION_SUPPORT}"
//	implementation "com.android.support:cardview-v7:${VERSION_SUPPORT}"
	implementation "com.android.support:recyclerview-v7:${VERSION_SUPPORT}"
	//noinspection GradleDependency 0.3.1+ is officially API 14+ (https://github.com/google/flexbox-layout/issues/390)
	//noinspection GradleDependency 0.3.1+ is strictly API 11+ (FlexboxHelper.calculateFlexLines calls View.getMeasuredState)
	//noinspection GradleDependency 0.3.0+ is strictly API 17+ (FlexboxHelper.getPaddingStartMain calls View.getPaddingStart)
	implementation('com.google.android:flexbox:0.3.0') { // 0.3.0 polyfilled as FlexboxLayoutCompat to API 10+
		// TODEL fixed in 0.3.2
		exclude group: 'com.android.support'
	}
	implementation "org.slf4j:slf4j-api:${VERSION_SLF4J}"

	// Class.forName("org.apache.xml.serializer.Version").getDeclaredMethod("main", String[].class)
	// .invoke(null, new Object[] { new String[0] }); // shows "Serializer Java 2.7.1" on Genymotion 2.3.7 and S5 5.0.0
//	compileOnly "xalan:xalan:2.7.1"

//	debugImplementation 'com.facebook.stetho:stetho:1.3.1'
//	debugImplementation 'com.facebook.stetho:stetho-js-rhino:1.3.1'
//	debugImplementation 'com.idescout.sql:sqlscout-server:1.0'

	apply from: "${rootDir}/gradle/testCompile.gradle", to: project

	androidTestImplementation "net.twisterrob.lib:twister-lib-android-espresso"
	androidTestImplementation "net.twisterrob.lib:twister-lib-android-espresso_actors"
	androidTestImplementation "net.twisterrob.lib:twister-lib-android-espresso_glide3"
	androidTestImplementation "net.twisterrob.lib:twister-lib-android-cpsuite"
	androidTestImplementation "net.twisterrob.lib:twister-lib-android-uiautomator"
	androidTestImplementation "net.twisterrob.lib:twister-lib-android-about-test_helpers"
	androidTestImplementation "net.twisterrob.lib:twister-lib-android-capture_image-test_helpers"
	androidTestImplementation project(':android:database:test_helpers')
}

android {
	defaultConfig {
		minSdkVersion 10
		targetSdkVersion 19
		applicationId 'net.twisterrob.inventory'
		version {
			major = 1
			minor = 1
			patch = 3
		}
		multiDexEnabled = true
		resConfigs "en_GB", "en_NZ", "en_AU" //, "hu"
		testInstrumentationRunner 'net.twisterrob.android.test.junit.AndroidJUnitRunner'
	}
	lintOptions {
		checkAllWarnings = true
		checkDependencies = true
	}
	testOptions.unitTests.includeAndroidResources = true // Robolectric
	testBuildType "debug" // CONSIDER release
	buildTypes {
		release {
			//debuggable true
			shrinkResources true // http://tools.android.com/tech-docs/new-build-system/resource-shrinking
		}
		debug {
			//minifyEnabled true
			multiDexKeepProguard file('src/androidTest/multidex.pro')
		}
	}
	aaptOptions {
		noCompress 'svg' // Resources.openRawResourceFd can only open AssetFileDescriptor for uncompressed resources
		def defaultIgnoreAssetsPattern = '!.svn:!.git:!.ds_store:!*.scc:.*:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~'
		ignoreAssetsPattern defaultIgnoreAssetsPattern + ':!descript.ion:!*.yuml:!*.yuml.png'
	}
	sourceSets.test.java.srcDir 'src/test/robolectricJava'

	packagingOptions {
		/* Execution failed for task ':transformResourcesWithMergeJavaResForDebugAndroidTest'.
			> com.android.build.api.transform.TransformException: com.android.builder.packaging.DuplicateFileException:
			Duplicate files copied in APK META-INF/maven/com.google.guava/guava/pom.xml
			File1: I:\build\intermediates\exploded-aar\com.android.support.test.espresso\espresso-web\2.2.2\jars\classes.jar
			File2: I:\build\intermediates\exploded-aar\com.android.support.test.espresso\espresso-core\2.2.2\jars\classes.jar
		 */
		exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
		exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
	}
}

allprojects { project ->
	project.afterEvaluate {
		project.android.lintOptions.baseline rootProject.file("config/lint/lint-baseline-${project.name}.xml")
		project.android.lintOptions.lintConfig = rootProject.file("config/lint/lint.xml")
	}
}

subprojects { project ->
	project.afterEvaluate {
		//noinspection UnnecessaryQualifiedReference
		com.android.build.gradle.api.AndroidSourceSet androidTest = project.android.sourceSets.androidTest
		if (androidTest.java.sourceFiles.empty) {
			logger.info "Disabling AndroidTest tasks in ${project.path} as it has no sources in ${androidTest.java.srcDirs}"
			project.tasks.whenTaskAdded { task ->
				if (task.name.contains("AndroidTest")) {
					task.enabled = false
				}
			}
		}
	}
}

def upgradeTest = tasks.register("upgradeTest", UpgradeTestTask) { task ->
	task.enabled = false // TODO not working since AGP 3.3/3.4
}
tasks.connectedCheck.configure { it.dependsOn(upgradeTest) }

task generateHtmlReportFromXml(type: net.twisterrob.gradle.android.tasks.TestReportGenerator) {
	input = new File(rootDir, 'temp/tests')
	output = new File(rootDir, 'temp/tests_results')
	outputs.upToDateWhen { false }
}
