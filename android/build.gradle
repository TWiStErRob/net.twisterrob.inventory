apply plugin: 'com.android.application'
apply plugin: 'net.twisterrob.android-app'
apply from: new File(rootDir, 'gradle/mapping.gradle')

apply plugin: InventoryDatabasePlugin

databaseEntities {
	categories {
		def ss = android.sourceSets.main
		input = file(new File(ss.res.srcDirs.first(), 'values/strings_Categories.xml'))
		output = file(new File(ss.assets.srcDirs.first(), 'MagicHomeInventory.data.Categories.sql'))
		iconFolder = file(new File(ss.res.srcDirs.first(), 'raw'))
		conversion = "SQL"
	}
}
android.applicationVariants.all { com.android.build.gradle.api.ApplicationVariant variant ->
	variant.mergeAssets.dependsOn tasks.generateDataBase
}

dependencies {
	//configurations.implementation.resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // -SNAPSHOT
	implementation('net.twisterrob.lib:twister-libs-android:1.0-SNAPSHOT')
	implementation "net.twisterrob.lib:twister-lib-android-glide3"
	implementation('net.twisterrob.lib:twister-libs-java:1.0-SNAPSHOT')
	implementation 'net.twisterrob.lib:twister-lib-android-basics'
	implementation 'net.twisterrob.lib:twister-lib-android-widgets'
	implementation 'net.twisterrob.lib:twister-lib-android-about'
	implementation 'net.twisterrob.lib:twister-lib-android-logging'
	implementation 'net.twisterrob.lib:twister-lib-android-stringers'
	implementation 'net.twisterrob.lib:twister-lib-android-capture_image'
	runtimeOnly('net.twisterrob.lib:twister-lib-android-slf4j:1.0-SNAPSHOT')

	// TOFIX otherwise VariantFragment descendants don't see Fragment and its methods (Cannot access FQCN errors in IDEA)
	implementation "com.android.support:support-v4:${VERSION_SUPPORT}"
	implementation "com.android.support:support-annotations:${VERSION_SUPPORT}"
	implementation "com.android.support:appcompat-v7:${VERSION_SUPPORT}"
	implementation "com.android.support:design:${VERSION_SUPPORT}"
//	implementation "com.android.support:cardview-v7:${VERSION_SUPPORT}"
	implementation "com.android.support:recyclerview-v7:${VERSION_SUPPORT}"
	//noinspection GradleDependency 0.3.1+ is officially API 14+ (https://github.com/google/flexbox-layout/issues/390)
	//noinspection GradleDependency 0.3.1+ is strictly API 11+ (FlexboxHelper.calculateFlexLines calls View.getMeasuredState)
	//noinspection GradleDependency 0.3.0+ is strictly API 17+ (FlexboxHelper.getPaddingStartMain calls View.getPaddingStart)
	implementation('com.google.android:flexbox:0.3.0') { // 0.3.0 polyfilled as FlexboxLayoutCompat to API 10+
		// TODEL fixed in 0.3.2
		exclude group: 'com.android.support'
	}
	implementation "org.slf4j:slf4j-api:${VERSION_SLF4J}"

	implementation 'com.caverock:androidsvg:1.2.1'

	// Class.forName("org.apache.xml.serializer.Version").getDeclaredMethod("main", String[].class)
	// .invoke(null, new Object[] { new String[0] }); // shows "Serializer Java 2.7.1" on Genymotion 2.3.7 and S5 5.0.0
//	compileOnly "xalan:xalan:2.7.1"

//	debugImplementation 'com.facebook.stetho:stetho:1.3.1'
//	debugImplementation 'com.facebook.stetho:stetho-js-rhino:1.3.1'
//	debugImplementation 'com.idescout.sql:sqlscout-server:1.0'

	apply from: "${rootDir}/gradle/testCompile.gradle", to: project

	androidTestImplementation "net.twisterrob.lib:twister-lib-android-espresso"
	androidTestImplementation "net.twisterrob.lib:twister-lib-android-espresso_actors"
	androidTestImplementation "net.twisterrob.lib:twister-lib-android-espresso_glide3"
	androidTestImplementation "net.twisterrob.lib:twister-lib-android-cpsuite"
	androidTestImplementation "net.twisterrob.lib:twister-lib-android-uiautomator"
	androidTestImplementation "net.twisterrob.lib:twister-lib-android-about-test_helpers"
	androidTestImplementation "net.twisterrob.lib:twister-lib-android-capture_image-test_helpers"
}

android {
	defaultConfig {
		minSdkVersion 10
		targetSdkVersion 19
		applicationId 'net.twisterrob.inventory'
		version {
			versionFile = null
			major = 1
			minor = 1
			patch = 3
		}
		multiDexEnabled = true
		resConfigs "en_GB", "en_NZ", "en_AU" //, "hu"
		testInstrumentationRunner 'net.twisterrob.android.test.junit.AndroidJUnitRunner'
	}
	lintOptions {
		checkAllWarnings = true
		checkDependencies = true
		baseline file("config/lint/lint-baseline.xml")
		//abortOnError false
	}
	testOptions.unitTests.includeAndroidResources = true // Robolectric
	testBuildType "debug" // CONSIDER release
	buildTypes {
		release {
			//debuggable true
			shrinkResources true // http://tools.android.com/tech-docs/new-build-system/resource-shrinking
		}
		debug {
			//minifyEnabled true
		}
	}
	aaptOptions {
		noCompress 'svg' // Resources.openRawResourceFd can only open AssetFileDescriptor for uncompressed resources
		def defaultIgnoreAssetsPattern = '!.svn:!.git:!.ds_store:!*.scc:.*:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~'
		ignoreAssetsPattern defaultIgnoreAssetsPattern + ':!descript.ion:!*.yuml:!*.yuml.png'
	}
	sourceSets.main.res.srcDir 'src/main/res-icons'
	sourceSets.test.java.srcDir 'src/test/robolectricJava'

	packagingOptions {
		/* Execution failed for task ':transformResourcesWithMergeJavaResForDebugAndroidTest'.
			> com.android.build.api.transform.TransformException: com.android.builder.packaging.DuplicateFileException:
			Duplicate files copied in APK META-INF/maven/com.google.guava/guava/pom.xml
			File1: I:\build\intermediates\exploded-aar\com.android.support.test.espresso\espresso-web\2.2.2\jars\classes.jar
			File2: I:\build\intermediates\exploded-aar\com.android.support.test.espresso\espresso-core\2.2.2\jars\classes.jar
		 */
		exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
		exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
	}
}
android.testVariants.all { com.android.build.gradle.api.TestVariant test ->
	def grantPermissions = project.task(type: Exec, "grant${test.name.capitalize()}Permissions") {
		enabled = false // TOFIX figure out a way to run this at the correct time
		mustRunAfter test.install // not dependsOn, because that wouldn't allow running the task by itself
		def permission = 'android.permission.SET_ANIMATION_SCALE'
		commandLine "adb shell pm grant ${test./*testedVariant.*/ applicationId} ${permission}".split(' ')
	}
	test.connectedInstrumentTest.dependsOn grantPermissions
}

apply from: "${rootDir}/gradle/upgradeTest.gradle"

task generateHtmlReportFromXml(type: net.twisterrob.gradle.android.tasks.TestReportGenerator) {
	input = new File(rootDir, 'temp/tests')
	output = new File(rootDir, 'temp/tests_results')
	outputs.upToDateWhen { false }
}
